plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api 'com.google.android.gms:play-services-ads:19.7.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

def repo = new File(rootDir, "docs")
apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'jp.co.jchankchan'
            artifactId 'playground-android-library'
            version '0.0.3'
            artifact "$buildDir/outputs/aar/mylibrary-release.aar"
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        appendDependency(dependenciesNode, it.group, it.name, it.version, 'runtime')
                    }
                }
                configurations.api.allDependencies.each {
                    if (it.name != 'unspecified') {
                        appendDependency(dependenciesNode, it.group, it.name, it.version, 'compile')
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("file://${repo.absolutePath}")
        }
    }
}

static def appendDependency(parentNode, groupId, artifactId, version, scope) {
    def node = parentNode.appendNode("dependency")
    node.appendNode("groupId", groupId)
    node.appendNode("artifactId", artifactId)
    node.appendNode("version", version)
    node.appendNode("scope", scope)
}